# Checks for recipes then runs the relevant function to create mountaineering gear

execute if score $crafted gm4_crafting matches 0 if score $slot_count gm4_crafting matches 7 if score $stack_size gm4_crafting matches ..1 if data storage gm4_custom_crafters:temp/crafter {Items:[{Slot:0b,id:"minecraft:iron_ingot"},{Slot:2b,id:"minecraft:iron_ingot"},{Slot:3b,id:"minecraft:iron_ingot"},{Slot:4b,id:"minecraft:iron_boots"},{Slot:5b,id:"minecraft:iron_ingot"},{Slot:6b,id:"minecraft:iron_ingot"},{Slot:8b,id:"minecraft:iron_ingot"}]} unless data storage gm4_custom_crafters:temp/crafter Items[3].tag.gm4_mountaineering run function gm4_mountaineering:new_skis
execute if score $crafted gm4_crafting matches 0 store success score $crafted gm4_crafting if score $slot_count gm4_crafting matches 3 if score $stack_size gm4_crafting matches ..1 if data storage gm4_custom_crafters:temp/crafter {Items:[{Slot:0b,id:"minecraft:tripwire_hook"},{Slot:3b,id:"minecraft:stick"},{Slot:6b,id:"minecraft:stick"}]} run loot replace block ~ ~ ~ container.0 loot gm4_mountaineering:crafting/poles
execute if score $crafted gm4_crafting matches 0 if score $slot_count gm4_crafting matches 4 if score $stack_size gm4_crafting matches ..1 if data storage gm4_custom_crafters:temp/crafter {Items:[{Slot:1b,id:"minecraft:chainmail_boots"},{Slot:3b,id:"minecraft:iron_nugget"},{Slot:4b,id:"minecraft:iron_nugget"},{Slot:5b,id:"minecraft:iron_nugget"}]} unless data storage gm4_custom_crafters:temp/crafter Items[0].tag.gm4_mountaineering run function gm4_mountaineering:new_crampons
