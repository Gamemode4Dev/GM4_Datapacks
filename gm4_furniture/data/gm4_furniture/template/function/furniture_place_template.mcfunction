# try to place the furniture
# @s = player who placed the furniture player head
# at the center of the placed block rotated along axis to face towards player or away from the wall
# run from place/resolve_id
# this function was generated by generate.py

# first perform checks to see if furniture fits where it was placed
scoreboard players set $valid_placement gm4_furniture_data 1

# set variables
scoreboard players set $dyeable gm4_furniture_data {{ dyeable }}
scoreboard players set $table gm4_furniture_data {{ table }}
scoreboard players set $custom_interaction gm4_furniture_data {{ custom_interaction }}
scoreboard players set $custom_placement gm4_furniture_data {{ custom_placement }}
scoreboard players set $length gm4_furniture_data {{ length }}
scoreboard players set $depth gm4_furniture_data {{ depth }}
scoreboard players set $height gm4_furniture_data {{ height }}
scoreboard players set $diagonal_placement_allowed gm4_furniture_data {{ allow_diagonal_placement }}

# check for diagonal placement
execute if score $diagonal_placement_allowed gm4_furniture_data matches 1 if block ~ ~ ~ player_head run function gm4_furniture:place/check_diagonal_placement
# store rotation in storage if a non-standard rotation was used
execute if score $rotation gm4_furniture_data matches 1 run data modify storage gm4_furniture:data Rotation set value [0.0f,0.0f]
execute if score $rotation gm4_furniture_data matches 2 run data modify storage gm4_furniture:data Rotation set value [90F,0F]
execute if score $rotation gm4_furniture_data matches 3 run data modify storage gm4_furniture:data Rotation set value [180F,0F]
execute if score $rotation gm4_furniture_data matches 4 run data modify storage gm4_furniture:data Rotation set value [-90F,0F]
# store dyed_color
data modify storage gm4_furniture:temp furniture_data.color set from block ~ ~ ~ components."minecraft:dyed_color"

# wall only furniture must be placed on a wall
scoreboard players set $wall_only gm4_furniture_data {{ wall_only }}
execute if score $wall_only gm4_furniture_data matches 1 unless score $wall_placement gm4_furniture_data matches 1 run scoreboard players set $valid_placement gm4_furniture_data 0

# ceiling only furniture must be placed on a ceiling
scoreboard players set $ceiling_only gm4_furniture_data {{ ceiling_only }}
execute if score $ceiling_only gm4_furniture_data matches 1 if block ~ ~1 ~ #gm4:replaceable run scoreboard players set $valid_placement gm4_furniture_data 0

# wall placed furniture is not allowed to have depth, if any size is bigger than 1 check if there is space
scoreboard players set $placement_blocked gm4_furniture_data 0
execute store success score $existing_interaction gm4_furniture_data positioned ~ ~-0.5001 ~ store result score $check_id gm4_furniture_id run scoreboard players get @e[type=interaction,tag=gm4_furniture.interaction,distance=..0.1,limit=1] gm4_furniture_id
execute if score $existing_interaction gm4_furniture_data matches 1 run scoreboard players set $valid_placement gm4_furniture_data 0
execute if score $valid_placement gm4_furniture_data matches 1 if score $length gm4_furniture_data matches 2.. run function gm4_furniture:place/check_size/length_prep
summon marker ~ ~ ~ {Tags:["gm4_furniture","gm4_furniture.marked_block","gm4_furniture.middle"]}
setblock ~ ~ ~ air
execute if score $valid_placement gm4_furniture_data matches 1 if score $depth gm4_furniture_data matches 2.. run function gm4_furniture:place/check_size/depth_prep
execute if score $valid_placement gm4_furniture_data matches 1 if score $height gm4_furniture_data matches 2.. run function gm4_furniture:place/check_size/height_prep
kill @n[type=marker,tag=gm4_furniture.middle,distance=..2]
execute if score $placement_blocked gm4_furniture_data matches 1 run scoreboard players set $valid_placement gm4_furniture_data 0
execute if score $placement_blocked gm4_furniture_data matches 1 run kill @e[type=marker,tag=gm4_furniture.marked_block]

# if placement is not valid cancel placement
execute if score $valid_placement gm4_furniture_data matches 0 run loot spawn ~ ~ ~ loot gm4_furniture:furniture/{{ category }}/{{ technical_id }}
execute if score $existing_interaction gm4_furniture_data matches 1 as @e[type=item_display,tag=gm4_furniture.display,distance=..8] if score @s gm4_furniture_id = $check_id gm4_furniture_id run function gm4_furniture:place/replace_furniture_block with entity @s item.components."minecraft:custom_data".gm4_furniture
execute if score $valid_placement gm4_furniture_data matches 0 run return run data modify entity @n[type=item,nbt={Age:0s},distance=..0.1] Item.components."minecraft:dyed_color" set from storage gm4_furniture:temp furniture_data.color

# spawn the furniture
summon item_display ~ ~-0.4999 ~ {Tags:["gm4_furniture","gm4_furniture.display","smithed.entity","smithed.strict","gm4_new_furniture"],CustomName:"gm4_furniture_display.{{ category }}.{{ technical_id }}",item:{id:"leather_horse_armor",count:1,components:{"minecraft:custom_data":{gm4_furniture:{furniture_id:"{{ category }}/{{ technical_id }}",block_id:"{{ block_id }}"}},"minecraft:item_model":"{{ item_model }}"}},item_display:head,Rotation:[0.0f,0.0f],transformation:{left_rotation:[0f,0f,0f,1f],right_rotation:[0f,0f,0f,1f],translation:[0f,0.5f,0f],scale:[{{ scale }}f,{{ scale }}f,{{ scale }}f]}}
summon interaction ~-0.0001 ~-0.5001 ~-0.0001 {Tags:["gm4_furniture","gm4_furniture.interaction","gm4_furniture.main","smithed.entity","smithed.strict","gm4_new_furniture"],height:1.0003f,width:1.0003f,response:1b}
setblock ~ ~ ~ {{ block_id }}

# add placement tags
execute if score $wall_only gm4_furniture_data matches 1 run tag @e[type=interaction,tag=gm4_new_furniture,distance=..8] add gm4_furniture.on_wall
execute if score $ceiling_only gm4_furniture_data matches 1 run tag @e[type=interaction,tag=gm4_new_furniture,distance=..8] add gm4_furniture.on_ceiling

# spawn extensions if they exist
execute at @e[type=marker,tag=gm4_furniture.marked_block] run summon interaction ~-0.0001 ~-0.5001 ~-0.0001 {Tags:["gm4_furniture","gm4_furniture.interaction","gm4_furniture.additional","smithed.entity","smithed.strict","gm4_new_furniture"],height:1.0003f,width:1.0003f,response:1b}
execute at @e[type=marker,tag=gm4_furniture.marked_block] run setblock ~ ~ ~ {{ block_id }}

# add custom interaction tags
execute if score $custom_interaction gm4_furniture_data matches 1 run tag @e[type=interaction,tag=gm4_new_furniture,distance=..8] add gm4_furniture.custom_interaction

# if furniture is a table reduce interaction height
execute if score $table gm4_furniture_data matches 1 as @e[type=interaction,tag=gm4_new_furniture,distance=..8] run data modify entity @s height set value 1f

# if furniture is dyeable set to basic white
execute if score $dyeable gm4_furniture_data matches 1 positioned ~ ~-0.4999 ~ run data modify entity @n[type=minecraft:item_display,tag=gm4_new_furniture,tag=gm4_furniture.display,distance=..0.1] item.components."minecraft:dyed_color" set from storage gm4_furniture:temp furniture_data.color
execute if score $dyeable gm4_furniture_data matches 1 run tag @e[type=interaction,tag=gm4_new_furniture,distance=..8] add gm4_furniture.dyeable

# if furniture is sittable spawn sitting item_displays at appropiate locations and add tag
scoreboard players set @e[type=interaction,tag=gm4_new_furniture,distance=..8] gm4_furniture_sit_height {{ sittable }}

# rotate furniture depending on rotation set by player (if rotation is 1 default rotation can be kept)
execute if score $rotation gm4_furniture_data matches 2.. as @e[tag=gm4_new_furniture,distance=..8] run data modify entity @s Rotation set from storage gm4_furniture:data Rotation

# custom placement
execute if score $custom_placement gm4_furniture_data matches 1 run function gm4_furniture:place/custom/furniture/{{ category }}/{{ technical_id }}

# mark block as placed and set id
playsound minecraft:block.barrel.close block @a[distance=..6] ~ ~ ~ 1 1.6
execute store result score @e[tag=gm4_new_furniture,distance=..8] gm4_furniture_id run scoreboard players add $next_id gm4_furniture_id 1
tag @e[tag=gm4_new_furniture,distance=..8] remove gm4_new_furniture

# cleanup
kill @e[type=marker,tag=gm4_furniture.marked_block]
